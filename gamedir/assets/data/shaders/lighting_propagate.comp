#version 430

layout(local_size_x = 25, local_size_y = 25, local_size_z = 1) in;

// CHUNK_SIZE / LIGHT_SIZE
uniform int light_scale;

// CHUNK_SIZE x CHUNK_SIZE
uniform layout(binding=0, rgba8) readonly image2D t_px;

// LIGHT_SIZE x LIGHT_SIZE
uniform layout(binding=6, r32f) writeonly image2D t_dst;

// (LIGHT_SIZE + 2) x (LIGHT_SIZE + 2)
uniform layout(binding=7, r32f) image2D t_work;

void prep();

void main() {
    ivec2 pos_light = ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y);
    ivec2 pos_work = pos_light + ivec2(1);
    ivec2 pos_chunk = pos_light * light_scale;

    vec4 px = imageLoad(t_px, pos_chunk);

    bool full_solid = true;
    for (int dx = 0; dx < light_scale; dx++) {
        for (int dy = 0; dy < light_scale; dy++) {
            if (imageLoad(t_px, pos_chunk + ivec2(dx, dy)).a < 1) {
                full_solid = false;
            }
        }
    }
        
    float damp_factor = full_solid ? 0.9 : 1.0;

    float f = 0.0;
    for (int iter = 0; iter < 25; iter++) {
        // memoryBarrier();
        barrier();

        f = imageLoad(t_work, pos_work).r;
        if (f < 0.1) f = 0.0;
        f = max(f, 0.95 * damp_factor * imageLoad(t_work, pos_work + ivec2(1, 0)).r);
        f = max(f, 0.925 * damp_factor * imageLoad(t_work, pos_work + ivec2(1, 1)).r);
        f = max(f, 0.95 * damp_factor * imageLoad(t_work, pos_work + ivec2(-1, 0)).r);
        f = max(f, 0.925 * damp_factor * imageLoad(t_work, pos_work + ivec2(-1, 1)).r);
        f = max(f, 0.95 * damp_factor * imageLoad(t_work, pos_work + ivec2(0, 1)).r);
        f = max(f, 0.925 * damp_factor * imageLoad(t_work, pos_work + ivec2(1, -1)).r);
        f = max(f, 0.95 * damp_factor * imageLoad(t_work, pos_work + ivec2(0, -1)).r);
        f = max(f, 0.925 * damp_factor * imageLoad(t_work, pos_work + ivec2(-1, -1)).r);

        // memoryBarrier();
        barrier();
        imageStore(t_work, pos_work, vec4(f, vec3(1.0)));
    }

    // memoryBarrier();
    barrier();
    imageStore(t_dst, pos_light, vec4(f, vec3(1.0)));

    // imageStore(t_dst, pos_light, vec4(full_solid ? 1.0 : 0.0, vec3(1.0)));
}